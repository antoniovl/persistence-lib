
apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

def getPublishProperties() {
    String home = System.getProperty("user.home")
    File publishFile = new File(home, ".m2/publish.properties")
    InputStream is = new FileInputStream(publishFile)
    Properties p = new Properties()
    p.load(is)
    return p
}

repositories {
    mavenCentral()
    def publish = getPublishProperties()
    maven {
        url publish["release.url"]
    }
}

def getCredentials() {
    String home = System.getProperty("user.home")
    File settingsFile = new File(home, ".m2/settings.xml")
    def xmlSlurper = new XmlSlurper()
    def output = xmlSlurper.parse(settingsFile)
    def entries = output."servers"."server"
    for (entry in entries) {
        if (entry."id".text() == "releases") {
            return [username: entry.username.text(), password: entry.password.text()]
        }
    }
    return [:]
}

uploadArchives {
    def credentials = getCredentials()
    def publish = getPublishProperties()
    repositories {
        mavenDeployer {
            repository(url: publish["release.url"]) {
                authentication(userName: credentials["username"], password: credentials["password"])
            }
        }
    }
}

dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    testCompile group: 'org.testng', name: 'testng', version: '6.11'
}

test {
    useTestNG()

    testLogging.showStandardStreams = true

    beforeTest { descriptor ->
        logger.lifecycle("* Running test: " + descriptor)
    }
}

